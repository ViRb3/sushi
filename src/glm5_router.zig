//! One-token GLM sigmoid router; source-derived FP32 GEMV and stable selection.
//! Adapted from oMLX 6745c39c (Apache-2.0); see NOTICE.
const std = @import("std");
const mlx = @import("mlx.zig");
const Arr = mlx.mlx_array;
const HEADER: [:0]const u8 =
    \\#include <metal_stdlib>
    \\#include <metal_simdgroup>
    \\using namespace metal;
    \\template<typename T,bool precise> inline T sigmoid(T x) {
    \\ auto y=1/(1+(precise?metal::precise::exp(metal::abs(x)):metal::exp(metal::abs(x))));
    \\ return x<0?y:1-y;
    \\}
;
const LOGITS: [:0]const u8 =
    \\
    \\  const uint lane = thread_index_in_simdgroup;
    \\  const uint sg = simdgroup_index_in_threadgroup;
    \\  const int tok = int(threadgroup_position_in_grid.y);
    \\  constexpr int RPS = ROWS_PER_SIMD;
    \\  const int out_row = (int(threadgroup_position_in_grid.x) * 4 + int(sg)) * RPS;
    \\  if (out_row >= E) {
    \\    return;
    \\  }
    \\  const device float* mat = w + size_t(out_row) * K;
    \\  const device T* xv = x + size_t(tok) * K;
    \\  float result[RPS];
    \\  for (int tm = 0; tm < RPS; tm++) {
    \\    result[tm] = 0.0f;
    \\  }
    \\  int bn = int(lane) * 4;
    \\  for (int i = 0; i < K / 128; ++i) {
    \\    float v_coeff[4];
    \\    for (int tn = 0; tn < 4; tn++) {
    \\      v_coeff[tn] = static_cast<float>(xv[bn + tn]);
    \\    }
    \\    int mat_offset = 0;
    \\    for (int tm = 0; tm < RPS; tm++) {
    \\      float inter[4];
    \\      for (int tn = 0; tn < 4; tn++) {
    \\        inter[tn] = mat[mat_offset + bn + tn];
    \\      }
    \\      for (int tn = 0; tn < 4; tn++) {
    \\        result[tm] += inter[tn] * v_coeff[tn];
    \\      }
    \\      mat_offset += K;
    \\    }
    \\    bn += 128;
    \\  }
    \\  for (int tm = 0; tm < RPS; tm++) {
    \\    for (ushort sn = 16; sn >= 1; sn >>= 1) {
    \\      result[tm] += simd_shuffle_down(result[tm], sn);
    \\    }
    \\  }
    \\  if (lane == 0) {
    \\    for (int tm = 0; tm < RPS; tm++) {
    \\      const int e = out_row + tm;
    \\      float sgm = sigmoid<float,PRECISE>(result[tm]);
    \\      float biased = sgm + bias[e];
    \\      sig[size_t(tok) * E + e] = sgm;
    \\      biased_out[size_t(tok) * E + e] = biased;
    \\    }
    \\  }
;
const SELECT: [:0]const u8 =
    \\
    \\  const uint lane = thread_index_in_simdgroup;
    \\  const int tok = int(threadgroup_position_in_grid.x);
    \\  constexpr int PER = (E + 31) / 32;
    \\  const device float* bz = biased + size_t(tok) * E;
    \\  const device float* sz = sig + size_t(tok) * E;
    \\  float vals[PER];
    \\  bool taken[PER];
    \\  for (int j = 0; j < PER; j++) {
    \\    const int e = j * 32 + int(lane);
    \\    vals[j] = e < E ? bz[e] : -INFINITY;
    \\    taken[j] = e >= E;
    \\  }
    \\  int picked[TOPK];
    \\  for (int r = 0; r < TOPK; r++) {
    \\    float best = -INFINITY;
    \\    int best_e = 0x7fffffff;
    \\    for (int j = 0; j < PER; j++) {
    \\      const int e = j * 32 + int(lane);
    \\      if (!taken[j] && !isnan(vals[j]) &&
    \\          (best_e == 0x7fffffff || vals[j] > best || (vals[j] == best && e < best_e))) {
    \\        best = vals[j];
    \\        best_e = e;
    \\      }
    \\    }
    \\    for (ushort off = 16; off >= 1; off >>= 1) {
    \\      float ob = simd_shuffle_xor(best, off);
    \\      int oe = simd_shuffle_xor(best_e, off);
    \\      const bool other_better = oe != 0x7fffffff &&
    \\          (best_e == 0x7fffffff || ob > best || (ob == best && oe < best_e));
    \\      if (other_better) {
    \\        best = ob;
    \\        best_e = oe;
    \\      }
    \\    }
    \\    if (best_e == 0x7fffffff) {
    \\      for (int j = 0; j < PER; j++) {
    \\        const int e = j * 32 + int(lane);
    \\        if (!taken[j] && e < best_e) {
    \\          best_e = e;
    \\        }
    \\      }
    \\      for (ushort off = 16; off >= 1; off >>= 1) {
    \\        best_e = min(best_e, simd_shuffle_xor(best_e, off));
    \\      }
    \\    }
    \\    picked[r] = best_e;
    \\    if ((best_e % 32) == int(lane)) {
    \\      taken[best_e / 32] = true;
    \\    }
    \\  }
    \\  if (lane == 0) {
    \\    float total = 0.0f;
    \\    float gathered[TOPK];
    \\    for (int r = 0; r < TOPK; r++) {
    \\      gathered[r] = sz[picked[r]];
    \\      total = gathered[r] + total;
    \\    }
    \\    for (int r = 0; r < TOPK; r++) {
    \\      float q = NORM ? gathered[r] / total : gathered[r];
    \\      float s = q * scaling[0];
    \\      indices[tok * TOPK + r] = uint(picked[r]);
    \\      scores[tok * TOPK + r] = s;
    \\    }
    \\  }
;

pub const Result = struct { indices: Arr, scores: Arr };
var logits_kernel: ?mlx.mlx_fast_metal_kernel = null;
var select_kernel: ?mlx.mlx_fast_metal_kernel = null;
var calls: usize = 0;
pub fn callCount() usize {
    return calls;
}
pub fn resetCallCount() void {
    calls = 0;
}
fn kernel(slot: *?mlx.mlx_fast_metal_kernel, name: [*:0]const u8, ins: []const [*:0]const u8, outs: []const [*:0]const u8, source: [:0]const u8) !mlx.mlx_fast_metal_kernel {
    if (slot.*) |k| return k;
    const iv = mlx.mlx_vector_string_new_data(ins.ptr, ins.len);
    defer _ = mlx.mlx_vector_string_free(iv);
    const ov = mlx.mlx_vector_string_new_data(outs.ptr, outs.len);
    defer _ = mlx.mlx_vector_string_free(ov);
    const k = mlx.mlx_fast_metal_kernel_new(name, iv, ov, source, HEADER, true, false);
    if (k.ctx == null) return error.MetalKernelCompileFailed;
    slot.* = k;
    return k;
}
const Configs = struct { logits: mlx.mlx_fast_metal_kernel_config, select: mlx.mlx_fast_metal_kernel_config, cached: bool };
const Key = struct { width: c_int, experts: c_int, top: c_int, norm: bool, dtype: mlx.mlx_dtype, precise: bool };
const Entry = struct { key: Key, cfg: Configs };
var cache: [8]?Entry = @splat(null);
fn configs(key: Key) !Configs {
    for (cache) |entry| if (entry) |e| {
        if (std.meta.eql(e.key, key)) return e.cfg;
    };
    const lc = mlx.mlx_fast_metal_kernel_config_new();
    errdefer _ = mlx.mlx_fast_metal_kernel_config_free(lc);
    const sc = mlx.mlx_fast_metal_kernel_config_new();
    errdefer _ = mlx.mlx_fast_metal_kernel_config_free(sc);
    for (0..2) |_| try mlx.check(mlx.mlx_fast_metal_kernel_config_add_output_arg(lc, &.{ 1, key.experts }, 2, .float32));
    try mlx.check(mlx.mlx_fast_metal_kernel_config_set_grid(lc, 128 * @divExact(key.experts, 4), 1, 1));
    try mlx.check(mlx.mlx_fast_metal_kernel_config_set_thread_group(lc, 128, 1, 1));
    try mlx.check(mlx.mlx_fast_metal_kernel_config_add_template_arg_dtype(lc, "T", key.dtype));
    inline for (.{ "K", "E", "ROWS_PER_SIMD", "PRECISE" }, .{ key.width, key.experts, @as(c_int, 1), @as(c_int, @intFromBool(key.precise)) }) |name, value| try mlx.check(mlx.mlx_fast_metal_kernel_config_add_template_arg_int(lc, name, value));
    try mlx.check(mlx.mlx_fast_metal_kernel_config_add_output_arg(sc, &.{ 1, 1, key.top }, 3, .uint32));
    try mlx.check(mlx.mlx_fast_metal_kernel_config_add_output_arg(sc, &.{ 1, 1, key.top }, 3, .float32));
    try mlx.check(mlx.mlx_fast_metal_kernel_config_set_grid(sc, 32, 1, 1));
    try mlx.check(mlx.mlx_fast_metal_kernel_config_set_thread_group(sc, 32, 1, 1));
    inline for (.{ "E", "TOPK", "NORM" }, .{ key.experts, key.top, @as(c_int, @intFromBool(key.norm)) }) |name, value| try mlx.check(mlx.mlx_fast_metal_kernel_config_add_template_arg_int(sc, name, value));
    for (&cache) |*entry| if (entry.* == null) {
        const result = Configs{ .logits = lc, .select = sc, .cached = true };
        entry.* = .{ .key = key, .cfg = result };
        return result;
    };
    return .{ .logits = lc, .select = sc, .cached = false };
}
fn apply(k: mlx.mlx_fast_metal_kernel, inputs: []const Arr, cfg: mlx.mlx_fast_metal_kernel_config, s: mlx.mlx_stream) ![2]Arr {
    const iv = mlx.mlx_vector_array_new_data(inputs.ptr, inputs.len);
    defer _ = mlx.mlx_vector_array_free(iv);
    var ov = mlx.mlx_vector_array_new();
    defer _ = mlx.mlx_vector_array_free(ov);
    try mlx.check(mlx.mlx_fast_metal_kernel_apply(&ov, k, iv, cfg, s));
    var out = [_]Arr{ mlx.mlx_array_new(), mlx.mlx_array_new() };
    errdefer for (out) |v| {
        _ = mlx.mlx_array_free(v);
    };
    for (&out, 0..) |*v, i| try mlx.check(mlx.mlx_vector_array_get(v, ov, i));
    return out;
}
pub fn route(s: mlx.mlx_stream, x: Arr, weight: Arr, bias: Arr, top: c_int, scale: f32, norm: bool) !?Result {
    if (!mlx.streamIsGpu(s) or x.ctx == null or weight.ctx == null or bias.ctx == null or !std.math.isFinite(scale) or scale <= 0) return null;
    const xs = mlx.getShape(x);
    const ws = mlx.getShape(weight);
    if (xs.len != 3 or xs[0] != 1 or xs[1] != 1 or ws.len != 2 or xs[2] != ws[1]) return null;
    const width = ws[1];
    const experts = ws[0];
    const dtype = mlx.mlx_array_dtype(x);
    if ((dtype != .bfloat16 and dtype != .float32) or mlx.mlx_array_dtype(weight) != .float32 or mlx.mlx_array_dtype(bias) != .float32 or !std.mem.eql(c_int, &.{experts}, mlx.getShape(bias)) or experts < 16 or experts > 1024 or @mod(experts, 16) != 0 or width <= 64 or width >= 16 * experts or @mod(width, 128) != 0 or top < 1 or top > experts or top > 32) return null;
    var ready = false;
    try mlx.check(mlx._mlx_array_is_available(&ready, weight));
    if (!ready) return null;
    const strides = mlx.mlx_array_strides(weight);
    if (strides[1] != 1 or strides[0] != @as(usize, @intCast(width))) return null;
    const precise = (try @import("glm5_kda_fused.zig").sigmoidFloatMode(s)) orelse return null;
    const cfg = try configs(.{ .width = width, .experts = experts, .top = top, .norm = norm, .dtype = dtype, .precise = precise });
    defer if (!cfg.cached) {
        _ = mlx.mlx_fast_metal_kernel_config_free(cfg.logits);
        _ = mlx.mlx_fast_metal_kernel_config_free(cfg.select);
    };
    const lk = try kernel(&logits_kernel, "sushi_glm_router_logits", &.{ "x", "w", "bias" }, &.{ "sig", "biased_out" }, LOGITS);
    const values = try apply(lk, &.{ x, weight, bias }, cfg.logits, s);
    defer for (values) |v| {
        _ = mlx.mlx_array_free(v);
    };
    const scaling = mlx.mlx_array_new_data(&scale, &[_]c_int{1}, 1, .float32);
    defer _ = mlx.mlx_array_free(scaling);
    const sk = try kernel(&select_kernel, "sushi_glm_router_select", &.{ "sig", "biased", "scaling" }, &.{ "indices", "scores" }, SELECT);
    const chosen = try apply(sk, &.{ values[0], values[1], scaling }, cfg.select, s);
    calls += 1;
    return .{ .indices = chosen[0], .scores = chosen[1] };
}
